Clear
New Circuit.2Bus_example
!  define a really stiff source
! basekv = rated line-to-line kV
! pu = Actual per unit at which the source is operating. Assumed balanced for all phases.
! MVAsc1 = 1-phase short circuit MVA. There is some ambiguity concerning the meaning of this quantity. For the DSS, it is defined as kVBase2 / Z1-phase where  
!        Z1-phase = 1/3 (2Z1+Z0)        Thus, unless a neutral reactor is used, it should be a number on the same order of magnitude as Mvasc3.
! MVAsc3 = 3-phase short circuit MVA= kVBase2 / ZSC
! basemva = Default value is 100. Base used to convert values specified with puZ1, puZ0, and puZ2 properties to ohms on kV base specified by BasekV property.

~ basekv=0.4   pu=0.9959  MVAsc1=1e6  MVAsc3=1e6 basemva=0.5

!Define Linecodes


New linecode.556MCM nphases=3 basefreq=50  ! ohms per 5 mile
!Lower triangular form with | = row: [11 | 21 22 | 31 32 33]
!Assume the line is transposed and symmetric because z_s same and z_m same for x,r,c
~ rmatrix = ( 0.1000 | 0.0400    0.1000 |  0.0400    0.0400    0.1000)
~ xmatrix = ( 0.0583 |  0.0233    0.0583 | 0.0233    0.0233    0.0583)
~ cmatrix = (50.92958178940651  | -0  50.92958178940651 | -0 -0 50.92958178940651  ) ! small capacitance


New linecode.4/0QUAD nphases=3 basefreq=50  ! ohms per 100ft
~ rmatrix = ( 0.1167 | 0.0467    0.1167 | 0.0467    0.0467    0.1167)
~ xmatrix = (0.0667  |  0.0267    0.0667  |  0.0267    0.0267    0.0667 )
~ cmatrix = (50.92958178940651  | -0  50.92958178940651 | -0 -0 50.92958178940651  )  ! small capacitance

!Define lines

New Line.OHLine  bus1=sourcebus.1.2.3  loadbus.1.2.3  linecode = 556MCM   length=1  ! 5 mile line
!New Line.Quad    Bus1=Primary.1.2.3  loadbus.1.2.3  linecode = 4/0QUAD  length=1   ! 100 ft

//------------------------------------------------------------------------------------------------------------\\
!Loads - single phase
! Three Phase Unbalanced Wye Connected Load
! ( 0.4 3 sqrt / ) = ( 0.4 / sqrt(3) ) This sets the base kv for a single phase load
! loadbus.1.0, '.0' connect each of the loads to the ground '0'
//-------------------------------------------------------------------------------------------------------------\\
New Load.L1 phases=1  loadbus.1.0   ( 0.4 3 sqrt / )   kW=9   kvar=3  model=1
New Load.L2 phases=1  loadbus.2.0   ( 0.4 3 sqrt / )   kW=6   kvar=3  model=1
New Load.L3 phases=1  loadbus.3.0   ( 0.4 3 sqrt / )   kW=6   kvar=3  model=1


Set voltagebases=[0.4]
Set tolerance=0.000001
set defaultbasefreq=50
Calcvoltagebases

Solve

Dump Load.L2 debug
